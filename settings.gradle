import org.gradle.util.GradleVersion
import org.gradle.api.GradleScriptException

// Minimum Gradle version for main build
def minGradleVersion = GradleVersion.version("4.4")
// Minimum Gradle version for builds of JavaFX 11 module
def minFxGradleVersion = GradleVersion.version("4.10")

rootProject.name = 'tdcoinj-parent'

if (GradleVersion.current().compareTo(minGradleVersion) < 0) {
    throw new GradleScriptException("tdcoinj build requires Gradle ${minGradleVersion} or later", null)
}

include 'core'
project(':core').name = 'tdcoinj-core'

include 'tools'
project(':tools').name = 'tdcoinj-tools'

include 'examples'
project(':examples').name = 'tdcoinj-examples'

if (GradleVersion.current().compareTo(minFxGradleVersion) >= 0 && JavaVersion.current().isJava11Compatible()) {
    System.err.println "Including wallettemplate because ${GradleVersion.current()} and Java ${JavaVersion.current()}"

    include 'wallettemplate'
    project(':wallettemplate').name = 'tdcoinj-wallettemplate'

} else {
    System.err.println "Skipping wallettemplate, requires ${minFxGradleVersion}+ and Java 11+, currently running: ${GradleVersion.current()} and Java ${JavaVersion.current()}"
}
